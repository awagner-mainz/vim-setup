" /***************************************************
" * Filename:    ~.vimrc
" * Description: Setup file for the editor vim
" * Author:      Andreas Wagner
" * Date:        Apr 21 2005
" * *************************************************/
" Header {{{ -----------------------------------------
" viele Anregungen von Michael Prokop's vimrc ¸bernommen
" (check out http://config.michael-prokop.at) Thanks to Michael
"  which in turn is based on the one of Sven Guckes   -->
"  http://www.guckes.net/setup/ - vimrc.forall. Thanks to Sven!
"

" In this file, there are the following sections
" - OPTIONS
" - My variables
" - ABBREVIATIONS
" - COMMANDS (+ AUTOCOMMANDS)
" - MAPPINGS
"   - Movements
"   - Appearance
"   - Buffers
"   - Saving
"   - Formatting
"   - Screen
"   - Misc
"   - Vimrc Editing
"   - Spellchecking
" - SOME DOCUMENTATION

" General TODO:
" Add DrChip Menu support to other thingies.
"   and rename it back to DrChip again.
" Add TeX thingies (to ftplugin?)
" }}}
" =============================================================================
" OPTIONS {{{
" =============================================================================
"
"
" in alphabetical order

set autoindent					" always set autonidenting on
set autoread					" reload if file changed on disc
set autowrite					" saves buffer before launching shellcommands etc.
set nobackup					" don't use backups (use versions?)
if has ('gui_running')
	set background=light			" my gvim uses bright background
	let psc_style = 'warm'
	colo ps_color
"	colo peachpuff
else
	set background=dark			" my xterm/screen is dark.
"	let psc_style = 'cool'
"	colo ps_color
        let xterm16_colormap	= 'soft'
	let xterm16_brightness	= 'high'		" 'high' is default
        colo xterm16
endif
set backspace=indent,eol,start	" backspace over everything in insert mode
set browsedir=buffer		" use dir of current buffer in file open dialog
set cinoptions=>s,{0,}0,?0,^0,:0,=s,p0,t0,+s,(0,)20,*30
set nocompatible				" relax vi compatibility
set confirm						" ask about unsaved buffers rather than fail on quit etc.
" set digraph		" for strange chars
set encoding=utf-8
set noerrorbells
set errorformat=%f:%l:%c:%m		" that's the way tex-wrapper reports
" set errorformat=%f:%l:%m,\"%f\"\\,\ line\ %l\:\ %m
set esckeys		" allow cursor keys in insert mode
" set foldcolumn=4			" display markers for folds in a small column of its own
set foldmethod=marker			" use braces as folding markers
" set foldtext=MyFoldText()		" TODO: Check it out!
set formatoptions=2tcq		" format text using indentation of the second line
set history=50
set hlsearch					" highlight the last used search pattern
set grepprg=grep\ -nH\ $*		" make grep always generate a filename
if has("unix")
    set guifont=Luxi\ Mono\ 11
else
endif
set ignorecase					" ignore case in searching
set incsearch					" do incremental searching
set nojoinspaces				" have only one space when lines are joined at the end of phrases
set keymodel=startsel,stopsel			" use windows' selection keys (Ctrl+Shift+Right etc.)
set keywordprg=pinfo
set laststatus=2				" always have a status line (1-only if 2 or more windows; 0-never)
set matchpairs=(:),{:},[:],<:>
set nomodeline
" set modeline
" set modelines=1
set mouse=a		" mouse in all modes
" set mousefocus				" no real reason for this
set mousehide					" hides mouse after characters are typed
" set mousemodel=popup_setpos	" Right mouse button pops up a menu. Shift-left extends selection
set mousemodel=popup			" Right mouse button pops up a menu. Shift-left extends selection
set number						" number my lines
set pastetoggle=<f11>		" toggle paste/formatting
set report=0		" show report when >=N lines have changed (0 -> always)
set ruler						" show the cursor position all the time
set scrolloff=0		" don't scroll in context at top/bottom of screen
set shiftround		" indent on rounds of shiftwidth
set shiftwidth=4		" number of spaces for autoindenting
set shortmess=aI		" abbreviate (all) status messages and skip intro
set showcmd						" display incomplete commands
set showmatch					" highlight matching braces
set showmode					" display current mode
set sidescroll=1		" scroll sideways in single-column steps
set sidescrolloff=20		" keep large horiz. context scrolled in
set smarttab		" chose wisely between tabwidth and indent-width
set smartindent		" yes, do it.
set suffixes=.bak,~,.o,.h,.info,.swp,.aux,.bbl,.blg,.dvi,.idx,.lof,.log,.lot,.pdf,.ps,.toc	" set suffixes to ignore in command-line completion
set nostartofline		" keep cursor at column on page commands
" Statuslines
"       One favourite:
" set   statusline=[%n]\ %f\ %(\ %M%R%H)%)\=Pos=<%l\,%c%V>\ %P\=ASCII=%b\ HEX=%B)%=(c)\ Michael\ Prokop
"       My favourite statusline is:
" set statusline=%<[%n]\ %f\ %y\ %r\ %1*%m%*%w%=%(Column:\ %c%V%)%4(%)%-10(Line:\ %l%)\ %4(%)%p%%\ %P\ \ \ \ \ \ ASCII=%b\ HEX=%B\ \ \ \ \ %=(c)\ Michael\ Prokop
"
" 040519 $HOME/.vim/plugin/minibufexpl.vim - see: http://www.vim.org/scripts/script.php?script_id=159
" Thomas Winkler modified this script and wrote an own function (see below):
"
set statusline=%<\ %{GetBufferList()}\ %y\ %1*%*%w%=%(Col:\ %c%V%)%2(%)%-1(Line:\ %l/%L%)\ \ \ %p%%\ \ \ ASCII=%b\ HEX=%B\ %=

if has("terminfo")
 set t_Co=8
 set t_Sf=[3%p1%dm
 set t_Sb=[4%p1%dm
else
 set t_Co=8
 set t_Sf=[3%dm
 set t_Sb=[4%dm
endif
set tabstop=4
if has("unix")
    set tags=~/.vim/vim.tags,~/tags,./tags;/    " where are tags files
else
    set tags='$VIM\vim.tags,$VIM\tags,.\tags;'
endif
" if &term =~ "xterm"
"   set term=xterm-16color
" endif
" set textwidth=72
set title			" display information in the title
" set ttyfast			" yes - local terminal is fast.
"       ttybuiltin: default value is on
"       When on, the builtin termcaps are searched before the external ones.
"       When off the builtin termcaps are searched after the external ones.
" set ttybuiltin
set whichwrap=b,s,<,>,[,]			" allow cursor keys to wrap to next line
set wildcharm=<C-Z>					" define char to be used as esc in macros etc.
set wildmenu						" use navigation in tab-opened auto-completion "menus"
set wildmode=longest,list
set winminheight=0			" don't display any lines of minimized windows
set winminwidth=0			" the same
set writebackup
set wrapmargin=10


" }}}
" =============================================================================
" My variable settings {{{
" =============================================================================
"

" TABSTOPS and other FORMATTING VALUES {{{ - - -
let my_tab=4
" }}}

" TAGLIST {{{ - - - - -
if has("unix")
    let Tlist_Ctags_Cmd='/usr/bin/ctags'      " Taglist's variable denoting ctags executable
endif
" }}}

" MUTT {{{ - - - - - - -
if has("unix")
    let g:aliases_file='~/.mutt/aliases'            " In order to work with mutt aliases in email-editing
endif
" }}}

" SPELLCHECKING {{{ - - - - - -

" vimspell {{{ - - - - - - -
let g:spell_executable="aspell"
" let g:spell_language_list="german,english,francais"   " vimspell languages
" let g:spell_language_list="de,en,fr"		" vimspell languages
" let g:spell_german_tex_iskeyword = "34,~,|,-" " characters occuring inside of words
let g:spell_auto_type=''                        " don't auto-spell
let g:spell_insert_mode=0						" setting this to one enables on-the-fly
												" spellchecking in insert mode, remapping
												" <space> key
nmap \sr <Plug>SpellCheck						" use \sr to run spellcheck instead of \ss
let spell_accept_map = "<Leader>s&"             " use \s& to save a word to user dict.
let spell_case_accept_map = "<Leader>s*"		" use \s* to save a word case-sensitively
nmap \sa <Plug>SpellProposeAlternatives			" use \sa to suggest alt. instead of \s?
let spell_ignore_map = "<Leader>si"				" use \si to temporarily ignore a spelling.
" }}}

" lhVimSpell {{{ - - - - - - - -
let g:VS_spell_prog = "aspell"
let g:VS_language = "de"
let g:unix_layer_installed = "1"                " yes, I have a "sort" even in windows...
let g:VS_map_leader = "<Leader>S"               " begin lhVimSpell's bindings with \S

" since we have let-modeline.vim loaded, we can use modelines like this one:
" VIM: let b:VS_language = 'deutsch'
" in our documents...

" }}}

" engspchk/gerspchk {{{ - - - -
if has("unix")
    let g:cvimsyn='~/.vim/CVIMSYN'				" for engspchk/gerspchk dictionaries
else
    let g:cvimsyn='$VIM\CVIMSYN'				" for engspchk/gerspchk dictionaries
endif
let g:spchklang='ger'							" for engspchk/gerspchk language selection
let g:spchksilent=1								" suppress sourcing and loading messages
" }}}

" }}}

" MRU {{{ - - - - -
" I am using "MRU" by Gergely Kontra <kgergely@mcl.hu>, not "mru" by
" Yegappan Lakshmanan.
" It is older, but I like it having the MRU list in the file dialogue, not
" generating a separate window. I have added the Exclusion Feature of
" "mru" to it, tho, so:
" By default, all the edited file names will be added to the MRU list. If you
" want to exclude file names matching a list of patterns, you can set the
" MRU_Exclude_Files variable to a list of Vim regular expressions. By default,
" this variable is set to an empty string. For example, to not include files
" in the temporary (/tmp, /var/tmp and d:\temp) directories, you can set the
" MRU_Exclude_Files variable to

let MRU_Exclude_Files = '^/tmp/.*\|^/var/tmp/.*'		" For Unix

if has("unix")
    let MRU_Exclude_Files = '^/tmp/.*\|^/var/tmp/.*'		" For Unix
else
    let MRU_Exclude_Files = '^d:\\temp\\.*\|c:\\temp\\.*'       " For MS-Windows
endif
let MRU_num = 6
" }}}

" }}}
" =============================================================================
" Abbreviations {{{
" =============================================================================
"

"  Moved the abbreviations *before* the mappings as
"  some of the abbreviations get used with some mappings.
"
"  Let's start of with some standard strings
"  like the alphabet and the digits:
"
"     Yalpha : The lower letter alphabet.
  iab Yalpha abcdefghijklmnopqrstuvwxyz
"
"     YALPHA : The upper letter alphabet.
  iab YALPHA ABCDEFGHIJKLMNOPQRSTUVWXYZ
"
"     Ydigit: The ten digits.
  iab Ydigit  1234567890
"
"     Yruler: A "ruler" - nice for counting the length of words.
  iab Yruler  1234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890

" Abbreviations for some important numbers:
  iab Npi 3.1415926535897932384626433832795028841972
  iab Ne  2.7182818284590452353602874713526624977573

" Abbreviations for some classic long words:
" and now a word with all three umlauts and the '√ü', too:
  iab YHRA "Heiz√∂lr√ºcksto√üabd√§mpfung"
"
"     Phrases.
"     Yanfs: Changing the Subject within a message.  (Hi, Monty Python!)
  iab Yanfs  And now for something completely different...

"     Ybtdt:  It's been dejavu all over again.
  iab Ybtdt   Been there, done that, got the tshirt.

"     Ywyot:  people who use the browser for email and news...
  iab Ywyot when your only tool is a hammer - every problem looks like a nail.

" classic pangrams (which include every letter of the alphabet):
  iab Ypangram Bei jedem klugen Wort von Sokrates rief Xanthippe zynisch: Quatsch!

" Inserting an ellipsis to indicate deleted text
"  iab  Yell  [...]
  iab  Yel  [...]<CR>

" Use when posting in newsgroup or writing mail-replies
  iab  Ysc <ESC>o[schnipp]<CR><ESC>k$i
"  vmap ,ell c[...]<ESC>

" now some "autocorrection"...
"
" some words I type often enough:
  iab Latex     LaTeX
  iab latex     LaTeX
  iab Tex       TeX

" typing smileys slows me down..
  iab BAH :-(
  iab SMILE :-)
  iab WINK  ;-)

" Correcting some typos.
  iab alos      also
  iab aslo      also
  iab acuh      auch
  iab bracuht   braucht
  iab doer      oder
  iab nciht     nicht
  iab selbts	selbst
  iab SIe       Sie
  iab ICh       Ich
  iab Addresse	Adresse
  iab aggressiv	agressiv
  iab M√∂glihckeit M√∂glichkeit
  iab M√∂glihckeiten M√∂glichkeiten

" Often used filenames - only needed these on the command line:
if has("unix")
  cab Mrc       ~/.mutt/main
  cab Vrc	~/.vim/.vimrc
else
  cab Vrc	$VIM\_vimrc
endif

" }}}
" =============================================================================
" COMMANDS {{{
" =============================================================================

"  Source general files {{{ ---------------------------------------------------
if has("unix")
    source /usr/share/vim/current/menu.vim
    source /usr/share/vim/current/syntax/syntax.vim
else
    source $VIM\vim63\menu.vim
    source $VIM\vim63\syntax.vim
endif

filetype plugin on		" launch plugins based on filetype
filetype indent on		" indent based on filetype
" }}}

"  Autocommands {{{ -----------------------------------------------------------

" init empty
autocmd!

" For all text files set 'textwidth' to 78 characters.
" autocmd FileType text setlocal textwidth=78

" Detect mutt tempfiles
au! BufRead,BufNewFile /tmp/mutt*   setfiletype mail


" When editing a file, always jump to the last known cursor position.
" Don't do it when the position is invalid or when inside an event handler
" (happens when dropping a file on gvim).
autocmd BufReadPost *
    \ if line("'\"") > 0 && line("'\"") <= line("$") |
    \   exe "normal g`\"" |
    \ endif
" }}}

" Syntax Hightlighting ON {{{ ------------------------------------------------

syntax on		" Add syntax highlighting
" }}}

" }}}
" =============================================================================
" MAPPINGS {{{
" =============================================================================
"
" -----------------------------------------------------------------------------
" mapping commands: {{{
" -----------------------------------------------------------------------------
" :map		Normal, Visual and Operator-pending
" :vmap		Visual
" :nmap		Normal
" :omap		Operator-pending
" :map!		Insert and Command-line
" :imap		Insert
" :cmap		Command-line

" :noremap	Normal, Visual and Operator-pending
" :vnoremap	Visual
" :nnoremap	Normal
" :onoremap	Operator-pending
" :noremap!	Insert and Command-line
" :inoremap	Insert

" }}}
" -----------------------------------------------------------------------------
" MOVEMENTS {{{
" -----------------------------------------------------------------------------
"

" remap escape key to <jj> so it can be typed faster
  imap jj <esc>

" in normal mode, use backspace to go a word backwards
  nmap  b

" let "G" go to the END of the last line
  map G G$

" move to the next on-screen line on wrapped lines
imap <silent> <Down> <C-O>gj
imap <silent> <Up> <C-O>gk
nmap <silent> <Down> gj
nmap <silent> <Up> gk

" windows navigation - - - - - - - - - - -
" move between split windows with <Ctrl-j> and <Ctrl-k> (horiz.) and <Ctrl-h>, <Ctrl-l> (vert.)
 map <C-H> <C-W>h
 map <C-J> <C-W>j
 map <C-K> <C-W>k
 map <C-L> <C-W>l
imap <C-H> <ESC><C-W>h
imap <C-J> <ESC><C-W>j
imap <C-K> <ESC><C-W>k
imap <C-L> <ESC><C-W>l

" or do it with Alt-Arrow keys
 map <silent> <M-Up> :wincmd k<CR>
 map <silent> <M-Down> :wincmd j<CR>
 map <silent> <M-Left> :wincmd h<CR>
 map <silent> <M-Right> :wincmd l<CR>
imap <silent> <M-Up> <ESC>:wincmd k<CR>
imap <silent> <M-Down> <ESC>:wincmd j<CR>
imap <silent> <M-Left> <ESC>:wincmd h<CR>
imap <silent> <M-Right> <ESC>:wincmd l<CR>

" - - - - - - - - - - - - - - - - - - - - - - - -
" move back in history with <C-[>
" nmap <C-[> <C-O>			" breaks insert and cursor keys somehow - FIXME.

" - - - - - - - - - - - - - - - - - - - - - - - -
" mode search for visually selected text with <g/> (forward) and <g?> (backw.)
function! VsearchPatternSave()
   let l:temp = @@
   normal gvy
   let @/ = substitute(escape(@@, '/'), "\n", "\\\\n", "g")
   let @@ = l:temp
   unlet l:temp
endfunction
vmap g/ :call VsearchPatternSave()<CR>/<C-R>/<CR>
vmap g? :call VsearchPatternSave()<CR>?<C-R>/<CR>

" Open a search for the word under the cursor in a second window
map ,s2 :let @/=expand("<cword>") <BAR> split <BAR> execute 'normal n'<CR>


" - - - - - - - - - - - - - - - - - - - - - - - -
" Visualmap plugin to toggle/move visible (book)marks as in UEdit
" I've left the defaults, but mention them here to be able to quickly
" look them up...
"
" map something	<Plug>Vm_toggle_sign
" default: <c-F2>
"
" map something	<Plug>Vm_goto_next_sign
" default: <F2>
"
" map something	<Plug>Vm_goto_prev_sign
" default: <s-F2>


" }}}
" -----------------------------------------------------------------------------
" APPEARANCE {{{
" -----------------------------------------------------------------------------
"

"  F11 toggles Paste in Insert mode (see above, set pastetoggle)

" map <F9> :emenu <C-Z>

" ,wrp toggles wrapping TODO: make this affect sidescroll settings.
  map ,wrp :set wrap!<CR>

" Switch between nlsearch and nohlsearc
  map ,hl :set invhlsearch<CR>

" Paralleles Editieren TODO: FIXME
" map <C-P> :vsplit<CR><C-M>:bn<CR><C-M>:set scrollbind<CR><C-M><C-W><C-W>:set scrollbind<CR><C-M><C-W><C-W>
  map <C-P> :vsplit<CR>:bn<CR>:set scrollbind<CR><C-W><C-W>:set scrollbind<CR><C-W><C-W>

" Set background dark. TODO: Switch dark/light
  map ,bg :if &background=="light"<CR>:set background=dark<CR>:else<CR>:set background=light<CR>:endif<CR><CR>

" Highlight always some keywords
  syn keyword TODO FIXME XXX


" Folds ------------------------------------------------------------------------

" FoldDigest - open a digest of folds in a new buffer - use folddigest.
imap ,fd <Plug>FoldDigest

" Toggle fold state between closed and opened.
"
" If there is no fold at current line, just moves forward.
" If it is present, reverse it's state.
fun! ToggleFold()
  if foldlevel('.') == 0
    normal! l
  else
    if foldclosed('.') < 0
      . foldclose
    else
      . foldopen
    endif
  endif
  " Clear status line
  echo
endfun
" Map this function to Space key.
nnoremap <space> :call ToggleFold()<CR>
" ...and to the dash as well.
nnoremap - :call ToggleFold()<CR>

" This will select a whole line of text or it will select everything in a fold.
  nmap ,y <S-v>y

" in-/decrease foldlevel (zoom out/in)
  map  ,zo          :set foldlevel-=1<CR>
  map  ,zi          :set foldlevel+=1<CR>

" TODO: Map something to save the foldview
" :mkview

" TODO: Map something to restore the foldview
" :loadview

" TODO: FIXME
function! MyFoldText()
  let line = getline(v:foldstart)
  let vlines = v:foldendline-v:foldstartline
  let sub = substitute(line, '/\*\|\*/\|{{{\d\=', '', 'g')		" }}}
  return  sub . "level: " . v:foldlevel . " [" . v:foldstartline . "-" .
			  \ v:foldendline . " (" . vlines . "lines)]"
endfunction


" }}}
" -----------------------------------------------------------------------------
" BUFFERS {{{
" -----------------------------------------------------------------------------

" Buffer Navigation
  map <C-N> <ESC>:next <CR>
  map <C-P> <ESC>:prev <CR>

"
" The plugin fileexplorer.vim offers a new buffer with a browser window.

" I have to define the function which is being executed on a selected file:
function! EditThatFile(File)
    exe 'edit '. a:File
endfunction
let FB_CallBackFunction="EditThatFile"

" I want to open the dir of the current file or of the working dir, if no file is present.
function! WhichDir()
    if expand("%") != ''
	return fnamemodify(expand("%"), ":p:h")
    else
	return getcwd()
    endif
endfunction

" I'm mapping ,fb to open that browser window:
map ,fb :exe 'call FB_OpenFileBrowser("' . WhichDir() . '")'<CR>


" }}}
" -----------------------------------------------------------------------------
" SAVING {{{
" -----------------------------------------------------------------------------
"

" When I let Vim write the current buffer I frequently mistype the
" command ":w" as ":W" - so I have to remap it to correct this typo:
nmap :W :w
nmap :W! :w!

" just so with q
nmap :Q :q
nmap :Q! :q!


" save with <Ctrl-S>
function! MySave()
 if expand("%") == ""
    browse confirm w
 else
    w
 endif
endfunction

if has("gui_running")
	map <silent> <C-S> :call MySave()<CR>
	imap <silent> <C-S> <ESC><C-S>i
else
	map <silent> <C-S> :w<CR>
	imap <silent> <C-S> <ESC>:w<CR>i
endif

" Save function for interaction with ROX-savebox
" unmapped as of yet
function! Save()
 let tmpname = tempname()
 let fname = expand('%')
 if fname == ''
      let fname = 'TextFile'
 endif
 silent exec 'write !savebox ' . fname . ' &gt; ' . tmpname
 let newname = system('cat ' . tmpname)
 let tmp = system('rm ' . tmpname)
 if tmpname != ''
      exec 'file ' . escape(newname, ' ')
      set nomodified
 endif
endfunction
command! Save call Save()
" map <F3> :Save<CR>

" A mapping to make a backup of the current file.
fun! WriteBackup()
       let fname = expand("%:p") . "__" . strftime("%Y_%m_%d_%H.%M.%S")
       silent exe ":w " . fname
       echo "Wrote " . fname
endfun
map ,bck :call WriteBackup()<CR>


" }}}
" -----------------------------------------------------------------------------
" FORMATTING {{{
" -----------------------------------------------------------------------------
"

" Paragraph auto-formatting - - - - - - - - - - - - - - - - - - - - - - -

" Format current paragraph with Ctrl-Q and go back to previous position.
" It inserts CTRL-Z at the current position to enable to rebound to the
" previous position within the text.  [Hello, Y. K. Puckett!]
 map <C-Q> i<C-Z><esc>gqip?<C-Z><CR>x
imap <C-Q>  <C-Z><esc>gqip?<C-Z><CR>xi
 map Q gqip

" reformat the paragraph taking textwidth from current cursor position
" map ,tw :set textwidth=<C-R>=col(".")<CR><C-Q>
" TODO: test my remembering version
 map ,tw :let temptw=&tw<CR>:set tw=<C-R>=col(".")<CR><C-Q>:set tw=temptw<CR>:unlet temptw<CR>

" Narrow/Widen current paragraph by adjusting the current textwidth
" and using internal formatting (TODO: try out, possibly use other keybindings?):
  map  ,<          :set tw-=2<CR>gqip
  vmap ,<     <esc>:set tw-=2<CR>gvgqgv
  map  ,>          :set tw+=2<CR>gqip
  vmap ,>     <esc>:set tw+=2<CR>gvgqgv

" Tabstops - - - - - - - - - - - - - - - - - - - - - - - - - -

" virtual tabstops using spaces
execute "set shiftwidth=".my_tab
execute "set softtabstop=".my_tab
set expandtab

" allow toggling between local and default mode
function! TabToggle()
    if &expandtab
        set shiftwidth=8
        set softtabstop=0
        set noexpandtab
    else
        execute "set shiftwidth=".g:my_tab
        execute "set softtabstop=".g:my_tab
        set expandtab
    endif
endfunction
map ,tt mz:execute TabToggle()<CR>'z


" ANDERES - - - - - - - - - - - - - - - - - - - - - - - - - - - -

" Quick insertion of an empty line in normal mode:
  nmap <CR> O<ESC>

" "tal" is the "trailer alignment" filter program  http://thomasjensen.com/software/tal/
  vmap ,tal !tal -p 1<CR>

" Remove <CR> that disturb
"  map ,w2u :%s///g
  map ,w2u :%s/\r$/\r/g<CR>

" Substitute runs of two or more space to a single space:
" ksr = "kill space runs"
nmap ,ksr  :%s/  \+/ /g
vmap ,ksr   :s/  \+/ /g

" Convert all blocks of blank lines (containing whitespace only)
"   into *one* empty line (within current visual):
" sqz = "squeeze blank lines"
"  map ,Sbl :g/^\s*$/,/[^ <C-I>]/-j
"  map ,Sbl :g/^\s*$/,/[^ \t]/-j
   map ,sqz :g/^\s*$/,/\S/-j

" Show or Trim Spaces at ends of lines or before tabstops
function! ShowSpaces( ...)
 let @/="\\v(\\s+$)|( +\\ze\\t)"
 let oldhlsearch=&hlsearch
 if !a:0
   let &hlsearch=!&hlsearch
 else
   let &hlsearch=a:1
 end
 return oldhlsearch
endfunction

function! TrimSpaces() range
" execute "normal i\<C-Z>\<ESC>"
 let oldhlsearch=ShowSpaces( 1)
" execute a:firstline.",".a:lastline."substitute ///gec"
 execute a:firstline.",".a:lastline."substitute /\\(^--\\)\\@<!\\s*$//ge"
 let &hlsearch=oldhlsearch
" execute "normal ?\<C-Z>\<CR>x"
endfunction

command! -bar -nargs=? ShowSpaces call ShowSpaces(<args>)
command! -bar -nargs=0 -range=% TrimSpaces execute "normal i\<C-Z>\<ESC>:<line1>,<line2>call TrimSpaces()\<CR>?\<C-Z>\<CR>x"
nnoremap ,sws     :ShowSpaces 1<cr>
nnoremap ,tws     :TrimSpaces<cr>
vnoremap ,tws     :TrimSpaces<cr>

set list listchars=tab:ª∑,trail:∑,extends:>
" autocmd FileWritePre   * :call TrimSpaces()
" autocmd FileAppendPre  * :call TrimSpaces()
" autocmd FilterWritePre * :call TrimSpaces()
" autocmd BufWritePre    * :call TrimSpaces()
" autocmd FileWritePre   * :TrimSpaces
" autocmd FileAppendPre  * :TrimSpaces
" autocmd FilterWritePre * :TrimSpaces
" autocmd BufWritePre    * :TrimSpaces


" Swapping words and character - - - - - - - - - - - - - - -

" swap characters:
    nmap <silent> ,swc    xph

" swap a word with the next word. Works even across punctuation ("abc = def")
"    nmap <silent> ,sw "_yiw:s/\(\%#\w\+\)\(\W\+\)\(\w\+\)/\3\2\1/<CR><C-O><C-L>
" Making vim handle word swapping across a newline is fairly straightforward:
    nmap <silent> ,sww    "_yiw:s/\(\%#\w\+\)\(\_W\+\)\(\w\+\)/\3\2\1/<CR><C-O><C-L>

" swap a word to-the-left and to-the-right:
    nnoremap <silent> ,swl \"_yiw?\w\+\_W\+\%#<CR>:s/\(\%#\w\+\)\(\_W\+\)\(\w\+\)/\3\2\1/<CR><C-O><C-L>
    nnoremap <silent> ,swr \"_yiw:s/\(\%#\w\+\)\(\_W\+\)\(\w\+\)/\3\2\1/<CR><C-O>/\w\+\_W\+<CR><C-L>

" swap paragraphs:
  nnoremap ,swp  {dap}p{


" Pasting - - - - - - - - - - - - - - - - - - -
"
" Make shift-insert work like in Xterm
    map! <S-Insert> <MiddleMouse>

" Make p in Visual mode replace the selected text with the "" register.
    vnoremap p <Esc>:let current_reg = @"<CR>gvs<C-R>=current_reg<CR><Esc>

" Occasionally I later want already written text parts to put in parenthesis.
" I use the following macro, which brackets previously visually selected text.
" I mapped it with ,(. TODO: Check it out.
"    vnoremap ,( <ESC>`>a)<ESC>`<i(<ESC>

" map <Alt-a> to fix an indent-broken paste
    imap <silent> <M-A> <ESC>u:set paste<CR>.:set nopaste<CR>gi


" copy text from one file to a different one
    nmap    _C      :!echo ""> ~/.vi_tmp<CR><CR>:w! ~/.vi_tmp<CR>
    vmap    _C      :w! ~/.vi_tmp<CR>
    nmap    _P      :r ~/.vi_tmp<CR>

" }}}
" -----------------------------------------------------------------------------
" SCREEN (the program) settings {{{
" -----------------------------------------------------------------------------
"

" Disable the suspend for ^Z.
" I use Vim under "screen" where a suspend would lose the
" connection to the terminal - which is what I want to avoid.
  map <C-Z> :shell

" Ctrl-A is not available when running screen, so map it to ";A"
  map ;A <C-A>

" edit screen-temporary file (exchange)
  map   ## :e /tmp/screen-exchange

" }}}
" -----------------------------------------------------------------------------
" MISC OTHER FUNCTIONS/ROUTINES {{{
" -----------------------------------------------------------------------------
"

" Vimfootnotes
    nmap ,fnh :FootnoteComs<CR>				" get help
    imap ,ifn <Plug>AddVimFootnote
    imap ,rfn <Plug>ReturnFromFootnote

" Cream-Multireplace - prompted search and replace across multiple files...
    imap ,sr <C-o>:call Cream_replacemulti()<CR>
    vmap ,sr :<C-u>call Cream_replacemulti()<CR>
    nmap ,sr      :call Cream_replacemulti()<CR>

" A macro which allows you to open a "shell" in a new window
" - enable it with :vish
if has("unix")
  map ,sh :so /usr/share/vim/current/macros/vish.vim<CR>
else
  map ,sh :so $VIM\vim63\macros\vish.vim<CR>
endif

" The following code will add a function heading and position your
" cursor just after Description so that one can document as one proceeds with
" code. TODO: another keybinding?
function! FileHeading()
  let s:line=line(".")
  call setline(s:line,"/***************************************************")
  call append(s:line,"* Filename:    ")
  call append(s:line+1,"* Description: ")
  call append(s:line+2,"* Author:      Andreas Wagner")
  call append(s:line+3,"* Date:        ".strftime("%b %d %Y"))
  call append(s:line+4,"* *************************************************/")
  unlet s:line
endfunction
map ,fh <esc>mz:execute FileHeading()<CR>`zjA

" Insert TimeStamp into files - very usefull!
" first add a function that returns a time stamp in the desired format
if !exists("*TimeStamp")
  fun TimeStamp()
    return "Time-stamp: <" . strftime("%d %b %Y %X") . " Andreas Wagner>"
  endfun
endif

" this function searches for an existing time stamp and updates it using the
" function declared above
if !exists("*UpdateTimeStamp")
  fun UpdateTimeStamp()
    if (match(getline(1),"Time-stamp: <.*>")) > 1
      exe "1,1 s/Time-stamp: <.*>/" . TimeStamp()
    endif
  endfun
endif
" add an autocommand to update an existing time stamp when writing the file
" It uses the functions above to replace the time stamp and restores cursor
" position afterwards (this is from the FAQ)
autocmd BufWritePre,FileWritePre *   ks|call UpdateTimeStamp()|'s
" abbreviation to manually enter a timestamp. Just type YTS in insert mode
iab YTS <C-R>=TimeStamp()<CR>
" finally a key mapping to manually insert a TimeStamp
map ,ts :call UpdateTimeStamp()<CR>

" open url in current line in browser (or current file)
function! Browser ()
  let line = getline (".")
  let line = matchstr (line, "http[^ ]*")
  execute "!opera ".line
" execute "!opera "%:p
endfunction
map ,www :call Browser ()<CR>

" function for bufferlist in statusline - written by Thomas Winkler
  function! GetBufferList()
    let l:numBuffers = bufnr('$')
    let l:i = 0
    let l:bufferList = ''
    while(l:i <= l:numBuffers)
	let l:i = l:i + 1
	" check if buffer is listed
	if(getbufvar(l:i, '&buflisted') == 1)
	    let l:bufName = bufname(l:i)
	    if(strlen(l:bufName))
		if bufwinnr(l:i) != -1
			let l:shortBufName = fnamemodify(l:bufName, ":p:~")
		else
			let l:shortBufName = fnamemodify(l:bufName, ":t")
		endif
		let l:shortBufName = substitute(l:shortBufName, '[][()]', '', 'g')
		" check if buffer is readonly
		if (getbufvar(l:i, '&readonly'))
		    let l:shortBufName = l:shortBufName . '|RO'
		endif
		" check if buffer is currently open in a window
		if bufwinnr(l:i) != -1
		    let l:bufferList = l:bufferList . '[' . l:i . ':' . l:shortBufName . ']*'
		else
		    let l:bufferList = l:bufferList . '|' . l:i . ':' . l:shortBufName . '|'
		endif
		" check if buffer is modified
		if(getbufvar(l:i, '&modified') == 1)
		    let l:bufferList = l:bufferList . '+'
		endif
		let l:bufferList = l:bufferList  . ' '
	    endif
	endif
    endwhile
    return bufferList
  endfunction


" some tricks with the vimtips database itself
" update local vimtips file:
function! VimTipsUpdate()
  exe "!wget http://www.vim.org/tips/tip_download.php?download=download -O /tmp/vimtips.txt"<CR>
  exe "!echo 'vim:foldmethod=marker:' >> /tmp/vimtips.txt"<CR>
  edit /tmp/vimtips.txt
  %s/\(^VimTip\)/\1\t{{{1/g
  sav! $HOME/.vim/after/doc/vimtips\[1\].txt
  bd
  bd
endfun
map ,vtu :call VimTipsUpdate()<CR>

map ,vto :view $HOME/.vim/after/doc/vimtips\[1\].txt<CR>

" additions to help menu:
an 9999.90 &Help.-sep3- <Nop>
an 9999.91 &Help.&VimTips :e $HOME/.vim/after/doc/vimtips\[1\].txt <CR><CR>


" }}}
" -----------------------------------------------------------------------------
" VIMRC-EDITING {{{
" -----------------------------------------------------------------------------
"
"

" use VV to display VIMRUNTIME (which :version doesn't do)
  nmap VV :echo $VIMRUNTIME<CR>

  if has("unix")
    let vimrc='~/.vimrc'
  else
    let vimrc='$VIM\_vimrc'
  endif
  nn  ,lrc :source <C-R>=vimrc<CR><CR>
  nn  ,erc :edit   <C-R>=vimrc<CR><CR>

" }}}
" -----------------------------------------------------------------------------
" SPELLCHECKING {{{
" -----------------------------------------------------------------------------
"
" I am using Mathieu Clabaut's VIMSPELL (script_id=465) {{{ -------------------
" loaded by default from the plugin dir.
" (for some of the mappings look above at MY VARIABLES.)
"
" \sc : write, check and highlight mistakes
" \sA : start Autospell mode
" \sq : stop Autospell mode and return to normal highlighting
" \sl : switch languages
" \sn : move cursor to the next     spelling error
" \sp : move cursos to the previous spelling error
" \sS : insert word into user dict. (case-sensitive)
" \ss : insert word into user dict. (case-insensitive)
" \st : accept for this session only
" \sa : look for alternative spellings of word under cursor
" }}}

" Second approach: Luc Hermitte's Vim-Spell (script_id=135) {{{ ---------------
" keybindings:
" <M-S>r to update te current documents'cache of misspellings and alternatives
" <M-S>s to highlight misspelledwords
" <M-S>n and <M-S>p to go to next/previous misspelling
" <M-S>L change Language
" <M-S>a and <M-S><tab> show alternatives/open corrector window
" <M-S>E Close Corrector window and un-highlight misspellings
" <M-S>i ignore misspelling
" <M-S>* add word to personal dict. (<M-S>& adds in lowercase)
" in the corrector window, when an alternative is selected:
" A to replace every occurence of the misspelled word
" B to replace every occurence in all opened buffers
" u to undo/redo the last action
" h toggle help
" }}}


" Third approach: Charles Campbell's ENGSPCHK/GERSPCHK (script_id=195) {{{ ----
" To check the english spelling I use the engspchk.vim
" To check the german spelling I use the modfification thereof: gerspchk.vim
" Included Maps:
"  \et : add  word under cursor into database (temporarily - ie. just this file)
"  \es : save word under cursor into database (permanently)  (requires $CVIMSYN)
"  \en : move cursor to the next     spelling error
"  \ep : move cursor to the previous spelling error
"  \ea : look for alternative spellings of word under cursor
"
" note the variable settings at the bottom of the MY VARIABLES section above.
" also note that you cannot unload it.

if has("unix")
  map ,esp <ESC>:let g:spchklang=eng<CR>:so /usr/share/vim/current/macros/engspchk.vim<CR>i
  map ,gsp <ESC>:let g:spchklang=ger<CR>:so /usr/share/vim/current/macros/gerspchk.vim<CR>i
else
  map ,esp <ESC>:let g:spchklang=eng<CR>:so $VIM\vim63\macros\engspchk.vim<CR>i
  map ,gsp <ESC>:let g:spchklang=ger<CR>:so $VIM\vim63\macros\gerspchk.vim<CR>i
endif
" }}}

" Fourth and final approach: just run aspell on current file {{{ -------------
" map <F7> :!aspell --mode=tex check % <CR> :e!<CR>
  map <F8> :w!<CR>:!aspell --lang=de check %<CR>:e! %<CR>
" }}}


" highlight double occurrencies of words {{{ - - - - -
" (from an idea of Ajit Thakkar)
" http://vim.sourceforge.net/tips/tip.php?tip_id=206
"
syn match texDoubledWord "\c\<\(\a\+\)\_s\+\1\>"
let b:show_doublons_active = 0

function! ShowDoublons()
  let !b:show_doublons_active
  if ("b:show_doublons_active" == 1)
    hi def link texDoubledWord Error
  else
    hi def link texDoubledWord NONE
  endif
endfunction
map ,dbl :call ShowDoublons()<CR>

" }}}
" }}}
"
" END OF MAPPINGS
"
" }}}
" =============================================================================
" MORE DOCUMENTATION {{{
" =============================================================================
"

" =============================================================================
" foldmethod:
"  {Visual}zf => Operator to create a fold
"  zi => enable/disable folds
"  zf => create fold
"  zv => view cursor-line
"  zd => delete one fold at the cursor
"  zD => delete all folds
"  za => toggle fold (open or close)
"  zc => close fold
"  zm => close more folds
"  zM => close all folds: set foldlevel to 0 (zero)
"  zo => open fold
"  zr => open more folds
"  zR => open all folds
"  zx => update folds: close manually opend folds

" =============================================================================
"
"        %               current file name
"        %<              current file name without extension
"        #               alternate file name for current window
"        #<              idem, without extension
"        #31             alternate file number 31
"        #31<            idem, without extension
"        <cword>         word under the cursor
"        <cWORD>         WORD under the cursor (see |WORD|)
"        <cfile>         path name under the cursor
"        <cfile><        idem, without extension
" =============================================================================
"
" Der aktuelle Status von vim, inklusive aller Optionen, der Commandhistory
" und der editierten Dateien, kann mit:
"         :mksession sessiondatei
" in die Datei sessiondatei gesichert werden. Mit dem Aufruf:
"         vim -S sessiondatei
" kann dieser Status restauriert werden, inklusive Cursorposition.


" }}}

"eof
